#Использовать cli
#Использовать fs
#Использовать "../core"
#Использовать "."

Перем Лог;

///////////////////////////////////////////////////////////////////////////////

Процедура ВыполнитьПриложение()

	Приложение = Новый КонсольноеПриложение(ПараметрыПриложения.ИмяПриложения(),
											"Приложение для обновления файла логов изменения по истории коммитов");
	Приложение.Версия("version", ПараметрыПриложения.Версия());

	Приложение.Опция("v verbose", Ложь, "вывод отладочной информация в процессе выполнении")
					.Флаговый()
					.ВОкружении("CHANGELOG_GENERATOR_VERSOBE");

   	Приложение.Опция("breakline", 		ЗначенияПоУмолчанию.Разделитель(),	"После какой надписи выводить новые записи");
	Приложение.Опция("release",			"X.X.X",							"Номер релиза");
	Приложение.Опция("settings",		Неопределено,						"Путь к YML файлу настроек структуры лога.");
	Приложение.Опция("filename",		ЗначенияПоУмолчанию.ФайлВывода(),	"Файл для вывода лога изменений");

	Приложение.Аргумент("FROM",			"",		"SHA или TAG откуда начинать парсить логи");
	Приложение.Аргумент("TO",			"",		"SHA или TAG откуда начинать парсить логи")
						.Обязательный(Ложь);		   

	Приложение.УстановитьОсновноеДействие(ЭтотОбъект);
	Приложение.Запустить(АргументыКоманднойСтроки);

КонецПроцедуры // ВыполнениеКоманды()

Процедура ВывестиВФайлЛога(ИмяФайла, ТекстЛога, Разделитель)


	Если ФС.ФайлСуществует(ИмяФайла) Тогда

		ТекстРедактирования = ПрочитатьСодержимоеФайла(ИмяФайла);

	Иначе

		ТекстРедактирования = Разделитель;

	КонецЕсли;

	ПозицияВывода = Найти(ТекстРедактирования, Разделитель) + СтрДлина(Разделитель);

	ТекстКВыводу = Лев(ТекстРедактирования, ПозицияВывода) 
						+ Символы.ПС 
						+ Символы.ПС
						+ ТекстЛога 
						+ Сред(ТекстРедактирования, ПозицияВывода + 1);

	ВывестиВФайл(ИмяФайла, ТекстКВыводу);

КонецПроцедуры

Функция ПрочитатьСодержимоеФайла(ПутьКФайлу)

	ЧтениеФайла = Новый ЧтениеТекста;
	ЧтениеФайла.Открыть(ПутьКФайлу, КодировкаТекста.UTF8);

	Текст = ЧтениеФайла.Прочитать();

	ЧтениеФайла.Закрыть();

	Возврат Текст;

КонецФункции

Процедура ВывестиВФайл(ПутьКФайлу, Содержимое)

	ЗаписьФайла = Новый ЗаписьТекста;
	ЗаписьФайла.Открыть(ПутьКФайлу, КодировкаТекста.UTF8);
	ЗаписьФайла.Записать(Содержимое);
	ЗаписьФайла.Закрыть();

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт

	ХешМеткаОт    				= КомандаПриложения.ЗначениеАргумента("FROM");
	ХешМеткаДо					= КомандаПриложения.ЗначениеАргумента("TO");
	
	ФайлНастроек				= КомандаПриложения.ЗначениеОпции("settings");
	НастрокиИзФайла				= ПарсерНастроек.ПрочитатьНастройки(ФайлНастроек);

	Разделитель					= КомандаПриложения.ЗначениеОпции("breakline");
	Если Разделитель = ЗначенияПоУмолчанию.Разделитель() 
			И НастрокиИзФайла["breakline"] <> Неопределено Тогда

		Разделитель = НастрокиИзФайла["breakline"];

	КонецЕсли;

	НомерРелиза 				= КомандаПриложения.ЗначениеОпции("release");

	ФайлВывода					= КомандаПриложения.ЗначениеОпции("filename");
	Если ФайлВывода = ЗначенияПоУмолчанию.ФайлВывода() 
			И НастрокиИзФайла["filename"] <> Неопределено Тогда

		Разделитель = НастрокиИзФайла["filename"];

	КонецЕсли; 

	ЛогИзменений 				= ПарсерГит.ПолучитьМассивКоммитов(ХешМеткаОт, ХешМеткаДо);
	
	СтруктураВывода				= Новый СтруктураЛога;
	СтруктураВывода.ИспользоватьНастройки(НастрокиИзФайла["massage_groups"]);

	ДеревоВывода				= СтруктураВывода.ПолучитьСтруктуруЛога();

	Генератор					= Новый ГенераторЛога(НомерРелиза);
	Генератор.ИспользоватьСтруктуруЛога(ДеревоВывода);

	ТекстЛога					= Генератор.СформироватьТекстовоеПредставлениеИсторииКоммитов(ЛогИзменений);

	ВывестиВФайлЛога(ФайлВывода, ТекстЛога, Разделитель);

	Лог.Информация("Файл %1 обновлен", ФайлВывода);

КонецПроцедуры // ВыполнитьКоманду

///////////////////////////////////////////////////////
Лог = ПараметрыПриложения.Лог();

Попытка

	ВыполнитьПриложение();

Исключение

	Лог.КритичнаяОшибка(ОписаниеОшибки());

	ЗавершитьРаботу(1);

КонецПопытки;